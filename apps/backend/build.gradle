plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.1'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.peekle'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot Starters
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // Swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // WebSocket용
    implementation 'io.github.springwolf:springwolf-ui:1.7.0' // UI 화면
    implementation 'io.github.springwolf:springwolf-core:1.7.0'
    // 사용하는 프로토콜에 따라 추가 (WebSocket, STOMP, Kafka 등)
    implementation 'io.github.springwolf:springwolf-stomp:1.7.0'
    // 또는 순수 웹소켓용 설정이 필요할 수 있음

    // Database
    runtimeOnly 'com.mysql:mysql-connector-j'
    implementation 'io.github.cdimascio:dotenv-java:3.0.0'

    // JWT
    implementation 'io.jsonwebtoken:jjwt-api:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.5'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // QueryDSL
    implementation 'com.querydsl:querydsl-jpa:5.1.0:jakarta'
    annotationProcessor 'com.querydsl:querydsl-apt:5.1.0:jakarta'
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // DevTools
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    runtimeOnly 'com.h2database:h2' // bootRun 시 사용
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'com.h2database:h2' // 테스트용 H2

    // Testcontainers
    testImplementation "org.testcontainers:junit-jupiter:1.19.3"
    testImplementation "org.testcontainers:mysql:1.19.3"

    // Jsoup
    implementation 'org.jsoup:jsoup:1.17.2'
}

tasks.named('test') {
    useJUnitPlatform()
}

// 환경별 실행을 위한 편의용 태스크 (GradleBuild 타입)
task runDev(type: GradleBuild) {
    group = 'application'
    description = 'Runs the application with the dev profile (H2, Flyway OFF).'
    tasks = ['bootRun']
    startParameter.projectProperties = [profile: 'dev']
}

task runProd(type: GradleBuild) {
    group = 'application'
    description = 'Runs the application with the prod profile (MySQL, Flyway ON).'
    tasks = ['bootRun']
    startParameter.projectProperties = [profile: 'prod']
}

// bootRun 실행 시 프로퍼티로 프로필을 받거나 기본값 사용
tasks.named('bootRun') {
    if (project.hasProperty('profile')) {
        systemProperty 'spring.profiles.active', project.property('profile')
    }
}

// QueryDSL 설정
def querydslDir = layout.buildDirectory.dir("generated/querydsl").get().asFile

sourceSets {
    main.java.srcDirs += [querydslDir]
}

tasks.withType(JavaCompile).configureEach {
    options.generatedSourceOutputDirectory = file(querydslDir)
}

clean {
    delete file(querydslDir)
}
